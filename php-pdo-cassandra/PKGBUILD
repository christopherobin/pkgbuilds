# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=php-pdo-cassandra-git
pkgver=60.d1a1d4b
pkgrel=1
pkgdesc="PHP extension for accessing cassandra through PDO using CQL"
arch=('i686' 'x86_64')
url="https://code.google.com/a/apache-extras.org/p/cassandra-pdo/"
license=('PHP')
depends=('php>=5.3.0' 'thrift')
backup=('etc/php/conf.d/pdo-cassandra.ini')
install=php-pdo-cassandra.install
source=("git+https://code.google.com/a/apache-extras.org/p/cassandra-pdo/")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/cassandra-pdo"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/cassandra-pdo"
  phpize
  ./configure
  make
}

package() {
  cd "${srcdir}/cassandra-pdo"
  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=pdo_cassandra.so' > "pdo-cassandra.ini"
  install -D -m644 "pdo-cassandra.ini" "${pkgdir}/etc/php/conf.d/pdo-cassandra.ini"
}
